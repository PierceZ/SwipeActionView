apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven-publish'


android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

publishing {
    publications {
        release(MavenPublication) {
            artifact(bundleReleaseAar)
            artifact sourcesJar
            artifact javadocJar

            groupId = 'com.github.Tunous'
            artifactId = 'SwipeActionView'
            version = '1.4.0-SNAPSHOT'
        }
    }
}


dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}


